#東方弾幕風[Single]
#ScriptVersion[3]
#Title["4面　道中会話A2"]
#Text["4面　道中会話A2"]
#Background[""]
#System["./../../../system/System.dnh"]

//---------------------------------------------------------------------

// 関数取り込み
#include"./../../../lib/lib_boss.dnh"
#include"./../../../lib/lib_talk.dnh"

@Event {
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)		{ SetScriptResult( 0);	}
	case(EV_REQUEST_TIMER)		{ SetScriptResult(-1);	}
	BossEvent();
}

@Initialize {
	// アニメリセット
	Reset_aTime(BOSS_ID_MOKOU);
	
	Boss_Init();	//初期設定
	
	objEnemy  = CreateBossTalk(BOSS_ID_MOKOU);
	Task_Main();
}

@MainLoop {
	yield;

}

// メインタスク
task Task_Main()
{
	// 魔法陣リセット
	BossEffect_ResetMagicCircle();

	// 会話準備
	StartTalk();

	wait(30);

	//会話処理
	FuncTalk();
	
	//次のシーンへ
	FinishTalk(true);

	AwayBoss();
}

// 会話処理
function FuncTalk()
{
	// オブジェクト作成
	let cutinBoss1   = CreateTalkObject(BOSS_ID_MOKOU, true);
	let cutinPlayer1 = CreateTalkObject(BOSS_ID_REIMU, false);
	
	// 初期設定
	ChangeTalkTexture(cutinPlayer1, I_NORMAL);
	SetImageFoward(cutinBoss1, true);
	BossScene_SetNameVisible(objScene, true);	//ボスネーム表示状態
	wait(15);
	
	let talkID = 0;
	loop
	{
		let talkType = FuncTalkSingle(talkID);
		if (talkType == 0) { break; }
		talkID = max(0, talkID+WaitNext());
	}

	// 会話処理単発（巻き戻しも出来るように）
	function FuncTalkSingle(talkID)
	{
		let talk_type = 1;
		alternative(talkID)
		case(0)
		{
			ChangeTalkTexture(cutinBoss1, I_SMILE);
SetTalkEx(cutinBoss1, "我可没有game over这个概念");
		}
		case(1)
		{
			ChangeTalkTexture(cutinPlayer1, I_DOYA);
SetTalkEx(cutinPlayer1, "那就从game over之前重来一次。[r]竹子为什么会发光？[r]竹林里为什么没有雨？");
		}
		case(2)
		{
			ChangeTalkTexture(cutinBoss1, I_NORMAL);
SetTalkEx(cutinBoss1, "其一是辉夜的结界的一个小应用。[r]永远亭似乎正在修理她们家房顶。");
		}
		case(3)
		{
			ChangeTalkTexture(cutinBoss1, I_NORMAL);
SetTalkEx(cutinBoss1, "其二则是，[r]隐藏在竹林中的麻烦的家伙所做的好事");
		}
		case(4)
		{
			ChangeTalkTexture(cutinBoss1, I_DOYA);
SetTalkEx(cutinBoss1, "从此往前，将是连我都[r]不好对付的迷宫");
		}
		case(5)
		{
			// ボス退室
			AwayBoss();
			SetImageVisible(cutinBoss1, false);
			
			ChangeTalkTexture(cutinPlayer1, I_DOYA);
SetTalkEx(cutinPlayer1, "要是迷了路高高飞起来不就行了。");
		}
		others
		{
			return(0);
		}
		return(talk_type);
	}
}

// 登場モーション
task AwayBoss()
{
	if (Obj_GetValueD(objEnemy, "BOSS_MOVED", false)) { return; }
	Obj_SetValue(objEnemy, "BOSS_MOVED", true);

	// 登場モーション
	ObjMove_SetDestAtWeightSpeedUp(objEnemy, CenX, 240, 30, 0.1, 0.1, 8);
	ascent(i in 0 .. 75)
	{
		let tmp = sin((i+1)/75*90);
		let tmp_s = 1 - tmp/2;
		let tmp_c = 255 - tmp*255;
		let tmp_a = min(255, (1-tmp)*1024);
		ObjRender_SetScaleXYZ(GetBossImageID(objEnemy), tmp_s, tmp_s, tmp_s);
		ObjRender_SetColor(GetBossImageID(objEnemy), tmp_c, tmp_c, tmp_c);
		ObjRender_SetAlpha(GetBossImageID(objEnemy), tmp_a);
		yield;
	}
	Obj_SetVisible(GetBossImageID(objEnemy), false);
	while(!FINISH_TALK) { yield; }
	Boss_Finalize(objEnemy, false, false);
}
