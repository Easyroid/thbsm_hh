#東方弾幕風[Single]
#ScriptVersion[3]
#Title["5面　ボス会話"]
#Text["5面　ボス会話"]
#Background[""]
#System["./../../../system/System.dnh"]

//---------------------------------------------------------------------

// 関数取り込み
#include"./../../../lib/lib_boss.dnh"
#include"./../../../lib/lib_talk.dnh"
#include"./lib_Nezu.dnh"

let objEnemy2;

@Event {
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)		{ SetScriptResult( 0);	}
	case(EV_REQUEST_TIMER)		{ SetScriptResult(-1);	}
	BossEvent();
}

@Initialize {
	Boss_Init();	//初期設定

	objEnemy  = CreateBossTalk(BOSS_ID_NEZU);
	Obj_SetVisible(GetBossImageID(objEnemy), false);	// ボス非表示

	Task_Main();
}

@MainLoop {
	yield;

}

// メインタスク
task Task_Main()
{
	// 魔法陣リセット
	BossEffect_ResetMagicCircle();

	StartTalk();

	wait(120);

	//会話処理
	FuncTalk();
	
	//次のシーンへ
	FinishTalk(true);
	Boss_Finalize(objEnemy, false, false);
}

// 会話処理
function FuncTalk()
{
	// オブジェクト作成
	let cutinBoss1   = CreateTalkObject(BOSS_ID_NEZU, true);
	let cutinPlayer1 = CreateTalkObject(BOSS_ID_MARISA, false);
	
	// 初期設定
	ChangeTalkTexture(cutinPlayer1, I_SMILE);
	ChangeTalkTexture(cutinBoss1, I_BORO1);
	SetImageFoward(cutinBoss1, true);
	wait(15);
	
	let talkID = 0;
	loop
	{
		let talkType = FuncTalkSingle(talkID);
		if (talkType == 0) { break; }
		talkID = max(0, talkID+WaitNext());
	}

	// 会話処理単発（巻き戻しも出来るように）
	function FuncTalkSingle(talkID)
	{
		let talk_type = 1;
		alternative(talkID)
		case(0)
		{
			ChangeTalkTexture(cutinBoss1, I_BORO1);
SetTalkEx(cutinBoss1, "怎么可能！我妖狐祢津！[r]竟又被区区人类！");
		}
		case(1)
		{
			ChangeTalkTexture(cutinPlayer1, I_SMILE);
SetTalkEx(cutinPlayer1, "抱歉，听说是星之魔力，[r]我就稍微拿走了一点。");
		}
		case(2)
		{
			ChangeTalkTexture(cutinBoss1, I_BORO1);
SetTalkEx(cutinBoss1, "祢津认输了。");
		}
		case(3)
		{
			ChangeTalkTexture(cutinBoss1, I_BORO2);
SetTalkEx(cutinBoss1, "想解决异变的话，[r]就去打倒身在魔宝城里的芙玛吧。");
		}
		case(4)
		{
			ChangeTalkTexture(cutinPlayer1, I_SURPRISE);
SetTalkEx(cutinPlayer1, "啊对了对了，芙玛是[r]打算封印你是吗？");
		}
		case(5)
		{
			ChangeTalkTexture(cutinBoss1, I_BORO2);
SetTalkEx(cutinBoss1, "反正你也要退治芙玛。[r]不然你就无法离开这个结界。");
		}
		case(6)
		{
			ChangeTalkTexture(cutinPlayer1, I_SMILE);
SetTalkEx(cutinPlayer1, "我会让芙玛教我操纵你的法术的。");
		}
		others
		{
			return(0);
		}
		return(talk_type);
	}
}

// 登場モーション
task EnterBoss()
{
	if (Obj_GetValueD(objEnemy, "BOSS_MOVED", false)) { return; }
	Obj_SetValue(objEnemy, "BOSS_MOVED", true);

	// 煙えへくと（集中）
	SetNezuSmoke(ObjMove_GetX(objEnemy2), ObjMove_GetY(objEnemy2), 2, true);
	CallSound(sndPower3);
	wait(60);
	RaiseBoss(objEnemy2, 30);	// 変身ボス登場
	CallSound(sndBoon);
}

// 変身モーション
task ChangeBoss()
{
	// 煙えへくと（拡散）
	SetNezuSmoke(ObjMove_GetX(objEnemy2), ObjMove_GetY(objEnemy2), 2, false);
	CallSound(sndPower2);
	wait(60);
	HideBoss(objEnemy2, 30);	// ボス隠れる
	wait(30);
	// 煙えへくと（集中）
	SetNezuSmoke(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 2, true);
	CallSound(sndPower3);
	wait(60);
	RaiseBoss(objEnemy, 30);	// 真ボス登場
	CallSound(sndBoon);
}

task RaiseBoss(objBase, timer)
{
	let objImg = GetBossImageID(objBase);

	Obj_SetVisible(objImg, true);
	ascent(i in 0 .. timer)
	{
		let tmp = (i+1)/timer*255;
		ObjRender_SetAlpha(objImg, tmp);
		yield;
	}
}

task HideBoss(objBase, timer)
{
	let objImg = GetBossImageID(objBase);

	Obj_SetVisible(objImg, true);
	descent(i in 0 .. timer)
	{
		let tmp = (i+1)/timer*255;
		ObjRender_SetAlpha(objImg, tmp);
		yield;
	}
	Obj_SetVisible(objImg, false);
}
