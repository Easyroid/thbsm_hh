#東方弾幕風[Single]
#ScriptVersion[3]
#Title["5面　ボス会話"]
#Text["5面　ボス会話"]
#Background[""]
#System["./../../../system/System.dnh"]

//---------------------------------------------------------------------

// 関数取り込み
#include"./../../../lib/lib_boss.dnh"
#include"./../../../lib/lib_talk.dnh"
#include"./lib_Nezu.dnh"

let objEnemy2;

@Event {
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)		{ SetScriptResult( 0);	}
	case(EV_REQUEST_TIMER)		{ SetScriptResult(-1);	}
	BossEvent();
}

@Initialize {
	Boss_Init();	//初期設定

	objEnemy  = CreateBossTalk(BOSS_ID_NEZU);
	Obj_SetVisible(GetBossImageID(objEnemy), false);	// ボス非表示

	Task_Main();
}

@MainLoop {
	yield;

}

// メインタスク
task Task_Main()
{
	// 魔法陣リセット
	BossEffect_ResetMagicCircle();

	StartTalk();

	wait(120);

	//会話処理
	FuncTalk();
	
	//次のシーンへ
	FinishTalk(true);
	Boss_Finalize(objEnemy, false, false);
}

// 会話処理
function FuncTalk()
{
	// オブジェクト作成
	let cutinBoss1   = CreateTalkObject(BOSS_ID_NEZU, true);
	let cutinPlayer1 = CreateTalkObject(BOSS_ID_REIMU, false);
	
	// 初期設定
	ChangeTalkTexture(cutinPlayer1, I_DOYA);
	ChangeTalkTexture(cutinBoss1, I_BORO2);
	SetImageFoward(cutinBoss1, true);
	wait(15);
	
	let talkID = 0;
	loop
	{
		let talkType = FuncTalkSingle(talkID);
		if (talkType == 0) { break; }
		talkID = max(0, talkID+WaitNext());
	}

	// 会話処理単発（巻き戻しも出来るように）
	function FuncTalkSingle(talkID)
	{
		let talk_type = 1;
		alternative(talkID)
		case(0)
		{
			ChangeTalkTexture(cutinBoss1, I_BORO2);
SetTalkEx(cutinBoss1, "無駄よ無駄！[r]禰津は敗れたが、これもまた計略通りぞ！");
		}
		case(1)
		{
			ChangeTalkTexture(cutinPlayer1, I_UPSET);
SetTalkEx(cutinPlayer1, "負け惜しみにしては気になる事を言うわね。");
		}
		case(2)
		{
			ChangeTalkTexture(cutinBoss1, I_BORO2);
SetTalkEx(cutinBoss1, "禰津はかの魔宝、かの呪いより[r]解き放たれる日を待っておった！");
		}
		case(3)
		{
			ChangeTalkTexture(cutinBoss1, I_BORO1);
SetTalkEx(cutinBoss1, "お主の邪魔立てで大三角星を操れなんだ、[r]その心残りももはや些事よ。");
		}
		case(4)
		{
			ChangeTalkTexture(cutinPlayer1, I_UPSET_SMILE);
SetTalkEx(cutinPlayer1, "あー、そう言えば夏の大三角の[r]真ん中って狐の星座だっけ。");
		}
		case(5)
		{
			ChangeTalkTexture(cutinBoss1, I_BORO2);
SetTalkEx(cutinBoss1, "最後の力が消える前に、[r]禰津が幻術にて世界を欺く。");
		}
		case(6)
		{
			ChangeTalkTexture(cutinBoss1, I_BORO2);
SetTalkEx(cutinBoss1, "禰津は巫女との戦いに敗れ散った。");
		}
		case(7)
		{
			ChangeTalkTexture(cutinBoss1, I_BORO2);
SetTalkEx(cutinBoss1, "礼を言うぞ、[r]これで禰津は自由を手に入れたのだ！");
		}
		case(8)
		{
			ChangeTalkTexture(cutinPlayer1, I_UPSET);
SetTalkEx(cutinPlayer1, "あのお城で欲霊が新しい願いを紡いでるなあ。[r]狐はまた封印だって。まあいいや黙っとこ");
			SetFukidashiType(cutinPlayer1, FUKIDASHI_THINK);	// 吹き出しタイプ変更
		}
		others
		{
			return(0);
		}
		return(talk_type);
	}
}

// 登場モーション
task EnterBoss()
{
	if (Obj_GetValueD(objEnemy, "BOSS_MOVED", false)) { return; }
	Obj_SetValue(objEnemy, "BOSS_MOVED", true);

	// 煙えへくと（集中）
	SetNezuSmoke(ObjMove_GetX(objEnemy2), ObjMove_GetY(objEnemy2), 2, true);
	CallSound(sndPower3);
	wait(60);
	RaiseBoss(objEnemy2, 30);	// 変身ボス登場
	CallSound(sndBoon);
}

// 変身モーション
task ChangeBoss()
{
	// 煙えへくと（拡散）
	SetNezuSmoke(ObjMove_GetX(objEnemy2), ObjMove_GetY(objEnemy2), 2, false);
	CallSound(sndPower2);
	wait(60);
	HideBoss(objEnemy2, 30);	// ボス隠れる
	wait(30);
	// 煙えへくと（集中）
	SetNezuSmoke(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 2, true);
	CallSound(sndPower3);
	wait(60);
	RaiseBoss(objEnemy, 30);	// 真ボス登場
	CallSound(sndBoon);
}

task RaiseBoss(objBase, timer)
{
	let objImg = GetBossImageID(objBase);

	Obj_SetVisible(objImg, true);
	ascent(i in 0 .. timer)
	{
		let tmp = (i+1)/timer*255;
		ObjRender_SetAlpha(objImg, tmp);
		yield;
	}
}

task HideBoss(objBase, timer)
{
	let objImg = GetBossImageID(objBase);

	Obj_SetVisible(objImg, true);
	descent(i in 0 .. timer)
	{
		let tmp = (i+1)/timer*255;
		ObjRender_SetAlpha(objImg, tmp);
		yield;
	}
	Obj_SetVisible(objImg, false);
}
